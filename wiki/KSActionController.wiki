#summary An action used to control other actions.
#labels Actions

=Action Controllers and Commands=
An action controller is a type of action that sends _commands_ to other [AbstractAction action] objects such as stop, start, or pause. The commands that can be executed are stored in the enumeration class `ActionControllerCommand`.  They include...
  * !ActionControllerCommand.KILL
  * !ActionControllerCommand.PAUSE
  * !ActionControllerCommand.RESET
  * !ActionControllerCommand.START
  * !ActionControllerCommand.STOP
  * !ActionControllerCommand.UNPAUSE

==Syntax==
{{{
new KSActionController(target:AbstractAction, command:ActionControllerCommand, offset:*);
}}}
  * `target` - the [AbstractAction] that will receive the commands from the controller.
  * `command` - the function that the [AbstractAction action] will perform when the KSActionController executes.
  * `offset` - the delay before the action executes


==Example==
The following example will pause a [Tween tween] partway through its execution and then unpause it after one second using two !KSActionControllers.
{{{
package
{
	import org.as3lib.kitchensync.easing.*;
	import org.as3lib.kitchensync.*;
	
	import flash.display.Sprite;
	
	public class KSActionControllerDemo extends Sprite
	{
		public function KSActionControllerDemo() {
			Synchronizer.initialize(this);
			
			// Draw a rectangle to use for demonstrations.
			var sprite:Sprite = new Sprite();
			sprite.graphics.beginFill(0);
			sprite.graphics.drawRect(0,0,20,20);
			addChild(sprite);
			
			// Tween the rectangle from 0 to 300 over 4 seconds.
			var tween:Tween = new Tween(sprite, "x", 300, 0, "4s", "0s", Cubic.easeInOut);
			// use action controllers to pause the tween at 2 seconds then unpause 1 second later.
			var pauseTween:KSActionController = new KSActionController(tween, ActionControllerCommand.PAUSE, "2s");
			var unpauseTween:KSActionController = new KSActionController(tween, ActionControllerCommand.UNPAUSE, "3s");
			// add all actions to a group and start the group.
			var group:KSParallelGroup = new KSParallelGroup(tween, pauseTween, unpauseTween);
			group.start();
		}
	}
}
}}}

==Related Links==
  * [http://mimswright.com/kitchensync/docs/api/com/mimswright/sync/KSActionController.html API Reference for KSActionController]
  * [http://mimswright.com/kitchensync/docs/api/com/mimswright/sync/ActionControllerCommand.html API Reference for ActionControllerCommand]